import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "5.1.0"
}

group 'net.wytrem.spigot'
version '1.1'
description = 'Friendships manager for spigot.'

String mainPackage = project.group + '.' + project.name.toLowerCase()
String apiVersion = '1.14'
String author = 'wytrem'
String website = 'https://www.spigotmc.org/resources/philios.78164/'

tasks.compileJava.sourceCompatibility = 1.8
tasks.compileJava.targetCompatibility = 1.8
tasks.compileJava.options.encoding = 'UTF-8'


repositories {
    mavenCentral()
    mavenLocal()
    //Spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    //Because Spigot depends on Bungeecord ChatComponent-API
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    // Pick only one of these and read the comment in the repositories block.
    compileOnly 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT' // The Spigot API with no shadowing. Requires the OSS repo.
    implementation 'net.wytrem.spigot.utils:core:+'
    implementation 'net.wytrem.spigot.utils:offers:+'

//    compileOnly 'org.bukkit:bukkit:1.14.4-R0.1-SNAPSHOT' // The Bukkit API with no shadowing.
//    compileOnly 'org.spigotmc:spigot:1.15.2-R0.1-SNAPSHOT' // The full Spigot server with no shadowing. Requires mavenLocal.
//    compileOnly 'org.bukkit:craftbukkit:1.14.4-R0.1-SNAPSHOT' // The full CraftBukkit server with no shadowing. Requires mavenLocal.
}

tasks.jar.enabled = false

tasks.processResources {
    filter ReplaceTokens, tokens: [
            "name"       : project.name,
            "mainPackage": mainPackage,
            "version"    : project.version,
            "apiVersion" : apiVersion,
            "description": project.description,
            "author"     : author,
            "website"    : website
    ]
}


shadowJar {
    relocate "net.wytrem.spigot.utils", "net.wytrem.spigot.philios.utils"
}
